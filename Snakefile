# Snakefile for processing scRNA-seq data with Snakemake and kallisto bustools
# mamba activate snakemake #Needs snakemake>=9.0
# snakemake --executor slurm --default-resources slurm_partition=medium slurm_time="2:00:00" runtime=120 mem_mb=8000 -j 16 -n 

import pandas as pd
import os

# Configuration
configfile: "config/config.yaml"
SCANPY_ENV = config["scanpy_env"]
CYCLUM_ENV = config["cyclum_env"]
KALLISTO_ENV = config["kallisto_env"]
SRA_TOOLS_ENV = config["sra_tools_env"]


# Load samples information
# Columns: 0=condition, 1=genome, 2=seq_platform, 3=replicate, 4=R1, 5=R2
samples_df = pd.read_csv(config["samples_file"], header=None, sep=',')
samples_df = samples_df.apply(lambda x: x.str.strip() if x.dtype == "object" else x)

# Change index format to use hyphen between condition and replicate
samples_df.index = [(row[0] + "-" + str(row[3]) + "_" + row[2]) for _, row in samples_df.iterrows()]

# Fail fast with a clear message if two rows collapse to the same sample_id
# (this happens when condition + replicate + seq_platform is duplicated in
# samples.csv, e.g. two rows with the same condition/replicate/platform but
# different genome). A duplicated index turns samples_df.loc[sample_id] into
# a DataFrame instead of a Series everywhere below, which surfaces as
# confusing errors like "unhashable type: 'Series'".
_dupe_ids = sorted(set(samples_df.index[samples_df.index.duplicated(keep=False)]))
if _dupe_ids:
    raise ValueError(
        f"Duplicate sample_id(s) in {config['samples_file']}: {_dupe_ids}. "
        "Each condition/replicate/seq_platform combination must be unique — "
        "check for duplicate rows or a reused replicate number for that condition/platform."
    )

# Get actual sample IDs that exist
SAMPLE_IDS = samples_df.index.tolist()

CONDITIONS = samples_df[0].unique().tolist()
REPLICATES = samples_df[3].unique().tolist()
SEQUENCING_PLATFORMS = samples_df[2].unique().tolist()

# Create a dictionary to track which condition-seq_platform combinations exist
# and what replicates are available for each
CONDITION_PLATFORM_REPS = {}
for sample_id in SAMPLE_IDS:
    condition = samples_df.loc[sample_id][0]
    seq_platform = samples_df.loc[sample_id][2]
    replicate = samples_df.loc[sample_id][3]

    key = (condition, seq_platform)
    if key not in CONDITION_PLATFORM_REPS:
        CONDITION_PLATFORM_REPS[key] = []
    CONDITION_PLATFORM_REPS[key].append(replicate)

# Get list of condition-seq_platform combinations that actually exist
CONDITION_PLATFORM_COMBOS = list(CONDITION_PLATFORM_REPS.keys())

# Find conditions that have BOTH 10x and pipseq data for method comparison
CONDITIONS_WITH_BOTH_METHODS = []
for condition in CONDITIONS:
    has_10x = any(p == '10x' for c, p in CONDITION_PLATFORM_COMBOS if c == condition)
    has_pipseq = any(p == 'pipseq' for c, p in CONDITION_PLATFORM_COMBOS if c == condition)
    if has_10x and has_pipseq:
        CONDITIONS_WITH_BOTH_METHODS.append(condition)

print(f"Conditions with both methods for comparison: {CONDITIONS_WITH_BOTH_METHODS}")

# Sample lookup by sample name; samples_df.loc['sample_name'][3] gives the R1 path
print(f"Conditions: {CONDITIONS}")
print(f"Replicates: {REPLICATES}")
print(f"Sequencing Platforms: {SEQUENCING_PLATFORMS}")
print(f"Sample IDs: {SAMPLE_IDS}")
print(f"Condition-Platform combinations: {CONDITION_PLATFORM_COMBOS}")

# Helper function to get fastq files for a sample
def get_fastq_files(sample_id):
    """Get R1 and R2 fastq files for a sample."""
    sample_info = samples_df.loc[sample_id]
    r1_path = sample_info[4]  # R1 path is in column 4 (0-indexed)
    r2_path = sample_info[5]  # R2 path is in column 5 (0-indexed)
    return r1_path, r2_path

# Helper function to get the reference genome name for a sample (looked up in config.yaml)
def get_genome(sample_id):
    """Get the genome name for a sample; used as a key into config.yaml for the kallisto index path."""
    sample_info = samples_df.loc[sample_id]
    return sample_info[1]  # Genome is in column 1 (0-indexed)

# Helper function to get replicates for a condition-seq_platform combo
def get_replicates_for_combo(condition, seq_platform):
    """Get the list of replicates that exist for a given condition-seq_platform combo."""
    key = (condition, seq_platform)
    return CONDITION_PLATFORM_REPS.get(key, [])

# Main rule that defines the final output
rule all:
    input:
        # Only create outputs for condition-seq_platform combos that exist
        # expand("results/combined/{condition}_{seq_platform}.h5ad",
        #        zip,
        #        condition=[c for c, p in CONDITION_PLATFORM_COMBOS],
        #        seq_platform=[p for c, p in CONDITION_PLATFORM_COMBOS]),
        # Use actual sample IDs for rRNA analysis
        # expand("results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam",
        #        sample_id=SAMPLE_IDS,
        #        gene=config.get("target_genes", ["GQX67_05945"])),
        # expand("results/rRNA_analysis/coverage/{sample_id}/{gene}_coverage.tsv",
        #        sample_id=SAMPLE_IDS,
        #        gene=config.get("target_genes", ["GQX67_05945"])),
        # expand("results/rRNA_analysis/blast/{sample_id}/{gene}.blast.summary",
        #        sample_id=SAMPLE_IDS,
        #        gene=config.get("target_genes", ["GQX67_05945"])),
        # expand("results/rRNA_analysis/plots/coverage_{condition}_{seq_platform}",
        #        zip,
        #        condition=[c for c, p in CONDITION_PLATFORM_COMBOS],
        #        seq_platform=[p for c, p in CONDITION_PLATFORM_COMBOS]),
        # expand("results/rRNA_analysis/plots/blast_{condition}_{seq_platform}",
        #        zip,
        #        condition=[c for c, p in CONDITION_PLATFORM_COMBOS],
        #        seq_platform=[p for c, p in CONDITION_PLATFORM_COMBOS]),
        # Integration
        "results/integrated/integrated.h5ad",
        # Cell cycle for uninfected samples
        "results/integrated/integrated_uninfected_with_cellcycle.h5ad",
        "results/integrated/integrated_uninfected_with_cellcycle_annotated/JW18_uninfected_cyclum_annotated.h5ad",
        # Validate PIPseq and 10X clustering
        "results/validate_pipseq/label_transfer_confusion_matrix.csv",
        "results/validate_pipseq/marker_gene_jaccard_matrix.csv",
        "results/validate_pipseq/pseudobulk_spearman_correlation.csv",
        # Cell cycle
        "results/cellcycle/.done",
        "results/integrated/integrated_by_cellcycle.h5ad",
        # Cluster marker + pathway analysis
        "results/cluster_marker_pathway/wolbachia_infection_markers_top50.csv",
        expand("results/sceptic/{sample}/sceptic_results_{sample}.csv",
            sample=["wolbachia_infection"]),
        expand("results/sceptic/{sample}/sceptic_{sample}.h5ad", 
            sample=["wolbachia_infection"]),
        expand("results/sceptic/{sample}/.done",
            sample=["wolbachia_infection"]),
                # Gene program and pathway analysis
        "results/nmf_programs/.done",
        "results/nmf_continuous_var/.done",
        "results/nmf_categorical_var/.done",
        "results/nmf_annotate_programs/program_cellcycle_overlap.csv",
        expand("results/pseudotime_genes/{sample}/summary_dynamic_genes.csv",
            sample=["wolbachia_infection"]),
        expand("results/pseudotime_genes/{sample}/.done",
            sample=["wolbachia_infection"]),
        expand("results/pseudotime_genes/{sample}/tradeseq.done",
            sample=["wolbachia_infection"]),
        expand("results/rRNA_analysis/read_counts/{sample_id}/{gene}_read_counts.txt",
               sample_id=SAMPLE_IDS,
               gene=config.get("target_genes", ["GQX67_05945"]))

rule clean_only:
    input:
        expand("results/annotated_h5ad/{sample_id}.h5ad", sample_id=SAMPLE_IDS),
        expand("results/rRNA_analysis/read_counts/{sample_id}/{gene}_read_counts.txt",
                sample_id=SAMPLE_IDS,
                gene=config.get("target_genes", ["GQX67_05945"]))


# Establish rule precedencex
ruleorder: map_pipseq > combine_files_by_condition_platform
ruleorder: map_10x > combine_files_by_condition_platform

# Process PIPseq samples with kallisto bustools
rule map_pipseq:
    input:
        read1 = lambda wildcards: get_fastq_files(wildcards.sample_id)[0],
        read2 = lambda wildcards: get_fastq_files(wildcards.sample_id)[1]
    output:
        h5ad = "results/h5ad_results/{sample_id}.h5ad",
        bus = "results/pipseq/{sample_id}/output.unfiltered.bus",
        ec = "results/pipseq/{sample_id}/matrix.ec",
        transcripts = "results/pipseq/{sample_id}/transcripts.txt"
    params:
        sample_id = "{sample_id}",
        outdir = "results/pipseq/{sample_id}",
        genome = lambda wildcards: get_genome(wildcards.sample_id),
        kallisto_index = lambda wildcards: os.path.join(config[get_genome(wildcards.sample_id)], "index.idx"),
        transcripts_to_genes = lambda wildcards: os.path.join(config[get_genome(wildcards.sample_id)], "t2g.txt")
    wildcard_constraints:
        sample_id = ".*_pipseq"  # Only match samples ending with _pipseq
    log:
        "logs/pipseq/{sample_id}.log"
    threads: 
        config["pipseeker_threads"]
    resources:
        slurm_partition = config["pipseeker_partition"],
        mem_mb = config["pipseeker_mem"],
        slurm_time = config["pipseeker_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting PIPseq processing for {params.sample_id}"
        echo "Input files: {input.read1}, {input.read2}"
        echo "Output directory: {params.outdir}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {KALLISTO_ENV}

        kb count \
            -i {params.kallisto_index} \
            --keep-tmp \
            -g {params.transcripts_to_genes} \
            -x 0,0,16:0,16,28:1,0,0 \
            -o {params.outdir} \
            -t {threads} \
            --h5ad \
            {input.read1} {input.read2} 

        echo "Moving h5ad file to final location"
        # Move the h5ad file to the expected location
        mv {params.outdir}/counts_unfiltered/adata.h5ad {output.h5ad}
        echo "PIPseq processing complete for {params.sample_id}"
        """

# Process 10X samples with kallisto bustools
rule map_10x:
    input:
        read1 = lambda wildcards: get_fastq_files(wildcards.sample_id)[0],
        read2 = lambda wildcards: get_fastq_files(wildcards.sample_id)[1]
    output:
        h5ad = "results/h5ad_results/{sample_id}.h5ad",
        bus = "results/10x/{sample_id}/output.unfiltered.bus",
        ec = "results/10x/{sample_id}/matrix.ec",
        transcripts = "results/10x/{sample_id}/transcripts.txt"
    params:
        sample_id = "{sample_id}",
        outdir = "results/10x/{sample_id}",
        genome = lambda wildcards: get_genome(wildcards.sample_id),
        kallisto_index = lambda wildcards: os.path.join(config[get_genome(wildcards.sample_id)], "index.idx"),
        transcripts_to_genes = lambda wildcards: os.path.join(config[get_genome(wildcards.sample_id)], "t2g.txt")
    wildcard_constraints:
        sample_id = ".*_10x"  # Only match samples ending with _10x
    log:
        "logs/10x/{sample_id}.log"
    threads:
        config["cellranger_threads"] 
    resources:
        slurm_partition = config["cellranger_partition"],
        mem_mb = config["cellranger_mem"],
        slurm_time = config["cellranger_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting 10x processing for {params.sample_id}"
        echo "Input files: {input.read1}, {input.read2}"
        echo "Output directory: {params.outdir}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {KALLISTO_ENV}
        
        kb count \
            --kallisto /private/home/jomojaco/kallisto/build/src/kallisto \
            -i {params.kallisto_index} \
            -g {params.transcripts_to_genes} \
            --keep-tmp \
            -x 10xv3 \
            -o {params.outdir} \
            -t {threads} \
            --h5ad \
            {input.read1} {input.read2} 

        echo "Moving h5ad file to final location"
        # Move the h5ad file to the expected locatio
        mv {params.outdir}/counts_unfiltered/adata.h5ad {output.h5ad}
        echo "10x processing complete for {params.sample_id}"
        """

# Filter h5ad output and output qc:
rule filter_h5ad:
    input: "results/h5ad_results/{sample_id}.h5ad"
    output:
        filtered_h5ad = "results/filtered_h5ad/{sample_id}.h5ad"
    params:
        script = config["filter_script"]
    log:
        "logs/filter/{sample_id}.log"
    threads:
        config["filter_threads"]
    resources:
        slurm_partition = config["filter_partition"],
        mem_mb = config["filter_mem"],
        slurm_time = config["filter_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting filtering for {wildcards.sample_id}"
        echo "Input file: {input}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --input {input} \
            --output {output.filtered_h5ad} 
        
        echo "Compressing original h5ad file"
        gzip {input}
        echo "Filtering complete for {wildcards.sample_id}"
        """

# Cell cycle Annotation
rule annotate_cell_cycle: # This needs the cyclum conda environment
    input:
        h5ad = "results/filtered_h5ad/{sample_id}.h5ad" # Output of filtered script 
    output:
        annotated_h5ad = "results/annotated_h5ad/{sample_id}.h5ad"
    params:
        script = config["cell_cycle_script"]
    log:
        "logs/annotate/{sample_id}.log"
    threads:
        config["cell_cycle_threads"]
    resources:
        slurm_partition = config["cell_cycle_partition"],
        mem_mb = config["cell_cycle_mem"],
        slurm_time = config["cell_cycle_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cell cycle annotation for {wildcards.sample_id}"
        echo "Input file: {input.h5ad}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {CYCLUM_ENV}

        python {params.script} \
            --input {input.h5ad} \
            --output {output.annotated_h5ad}
        
        echo "Compressing filtered h5ad file"
        gzip {input.h5ad}
        echo "Cell cycle annotation complete for {wildcards.sample_id}"
        """

rule combine_files_by_condition_platform:
    input:
        input_files = lambda wildcards: expand("results/annotated_h5ad/{condition}-{replicate}_{seq_platform}.h5ad", 
                                              condition=wildcards.condition, 
                                              replicate=get_replicates_for_combo(wildcards.condition, wildcards.seq_platform),
                                              seq_platform=wildcards.seq_platform)
    output:
        combined = "results/combined/{condition}_{seq_platform}.h5ad"
    params:
        combine_script = config["combine_script"],
        sample = '{condition}_{seq_platform}',  # Remove {replicate} since you're combining replicates
        fig_dir = 'results/combined/{condition}_{seq_platform}'  # Remove {replicate}
    log:
        "logs/combine/{condition}_{seq_platform}.log"
    threads: 
        config["combine_threads"]
    resources:
        slurm_partition = config["combine_partition"],
        mem_mb = config["combine_mem"],
        slurm_time = config["combine_time"]
    shell:
        """
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}
        python {params.combine_script} --files {input.input_files} --out_path {output.combined} --fig_dir {params.fig_dir} --sample {params.sample} --batch_key batch --min_cells 3 --min_genes 200
        """

##################################################################
# rRNA analysis rules
##################################################################
# Rule 1: Align extracted gene reads to rRNA reference with BWA
rule align_gene_reads:
    input:
        # r1 = "results/gene_extracted/{sample_id}/{gene}_R1.fastq.gz",
        r2 = lambda wildcards: get_fastq_files(wildcards.sample_id)[1],
        ref = config["ref_fasta"],
        regions = config["rRNA_regions"]
    output:
        bam = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam",
        bai = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam.bai"
    log:
        "logs/align_gene/{sample_id}_{gene}.log"
    threads: 8
    resources:
        slurm_partition = "medium",
        mem_mb = 16000,
        slurm_time = "2:00:00"
    shell:
        """
        # exec > {log} 2>&1
        echo "Starting BWA alignment for {wildcards.sample_id} - {wildcards.gene}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}

        # Make the directory
        mkdir -p results/rRNA_analysis/alignment/{wildcards.sample_id}
        
        # Align with BWA MEM, convert to BAM, sort, and filter for regions
        bwa mem -t {threads} {input.ref} {input.r2} | \
            samtools view -Sb | \
            samtools sort -@ {threads} -o results/rRNA_analysis/alignment/{wildcards.sample_id}/all_aligned.bam
        
        samtools index results/rRNA_analysis/alignment/{wildcards.sample_id}/all_aligned.bam
        
        # Extract reads for this specific gene using the full chromosome name
        CHROM=$(samtools idxstats results/rRNA_analysis/alignment/{wildcards.sample_id}/all_aligned.bam | grep "^{wildcards.gene}::" | cut -f1)
        
        if [ -z "$CHROM" ]; then
            echo "ERROR: Gene {wildcards.gene} not found"
            samtools idxstats results/rRNA_analysis/alignment/{wildcards.sample_id}/all_aligned.bam
            exit 1
        fi
        
        samtools view -b results/rRNA_analysis/alignment/{wildcards.sample_id}/all_aligned.bam "$CHROM" -o {output.bam}
        samtools index {output.bam}
        
        echo "Alignment complete for {wildcards.sample_id} - {wildcards.gene}"
        echo "Filtered to $(samtools view -c {output.bam}) reads in target regions"
        """

# Rule 2: Calculate coverage depth
rule calculate_coverage:
    input:
        bam = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam",
        bai = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam.bai"
    output:
        cov = "results/rRNA_analysis/coverage/{sample_id}/{gene}_coverage.tsv"
    log:
        "logs/coverage/{sample_id}_{gene}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 4000,
        slurm_time = "30:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Calculating coverage for {wildcards.sample_id} - {wildcards.gene}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        samtools depth {input.bam} > {output.cov}
        
        echo "Coverage calculation complete"
        """

# Rule 3: Extract 16S sequences from aligned BAM
rule extract_16s_sequences:
    input:
        bam = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam",
        bai = "results/rRNA_analysis/alignment/{sample_id}/{gene}_aligned.bam.bai"
    output:
        fasta = "results/rRNA_analysis/extracted_16S/{sample_id}/{gene}_16S.fasta"
    log:
        "logs/extract_16s/{sample_id}_{gene}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 4000,
        slurm_time = "30:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Extracting 16S sequences for {wildcards.sample_id} - {wildcards.gene}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        # Extract reads mapping to 16S region
        samtools fasta {input.bam} > {output.fasta}
        
        echo "16S extraction complete"
        """

# Rule 4: BLAST 16S sequences against database
rule blast_16s:
    input:
        fasta = "results/rRNA_analysis/extracted_16S/{sample_id}/{gene}_16S.fasta"
    output:
        blast = "results/rRNA_analysis/blast/{sample_id}/{gene}.blast"
    params:
        db = config["blast_db"]
    log:
        "logs/blast/{sample_id}_{gene}.log"
    threads: 16
    resources:
        slurm_partition = "medium",
        mem_mb = 32000,
        slurm_time = "4:00:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Running BLAST for {wildcards.sample_id} - {wildcards.gene}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        blastn -db {params.db} \
            -query {input.fasta} \
            -out {output.blast} \
            -num_threads {threads} \
            -outfmt 6 \
            -max_target_seqs 5 \
            -evalue 1e-5
        
        echo "BLAST complete"
        """

# Rule 5: Summarize BLAST results
rule summarize_blast:
    input:
        blast = "results/rRNA_analysis/blast/{sample_id}/{gene}.blast"
    output:
        summary = "results/rRNA_analysis/blast/{sample_id}/{gene}.blast.summary"
    log:
        "logs/summarize_blast/{sample_id}_{gene}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 2000,
        slurm_time = "15:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Summarizing BLAST results for {wildcards.sample_id} - {wildcards.gene}"
        
        # Summarize: count hits per subject, track best identity
        awk '{{count[$2]++; if($3 > best[$2]) best[$2]=$3}} 
             END {{for(s in count) printf "%6d %s (%.3f%% identity)\\n", count[s], s, best[s]}}' \
            {input.blast} | \
            sort -k1,1nr -k3,3nr > {output.summary}
        
        echo "BLAST summarization complete"
        """

# Rule 6: Plot coverage for samples grouped by condition and seq_platform
rule plot_coverage_by_group:
    input:
        coverage_files = lambda wildcards: expand(
            "results/rRNA_analysis/coverage/{condition}-{replicate}_{seq_platform}/{gene}_coverage.tsv",
            condition=wildcards.condition,
            replicate=get_replicates_for_combo(wildcards.condition, wildcards.seq_platform),
            seq_platform=wildcards.seq_platform,
            gene=config.get("target_genes", ["GQX67_05945"])
        )
    output:
        plot_dir = directory("results/rRNA_analysis/plots/coverage_{condition}_{seq_platform}")
    params:
        script = config["plot_coverage_script"]
    log:
        "logs/plot_coverage/{condition}_{seq_platform}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 8000,
        slurm_time = "1:00:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Plotting coverage for {wildcards.condition}_{wildcards.seq_platform}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        mkdir -p {output.plot_dir}
        
        # Create file list
        echo {input.coverage_files} | tr ' ' '\\n' > {output.plot_dir}/coverage_files.txt
        
        python {params.script} {output.plot_dir}/coverage_files.txt \
            --output-dir {output.plot_dir}
        
        echo "Coverage plotting complete"
        """

# Rule 7: Plot BLAST pie charts for samples grouped by condition and seq_platform
rule plot_blast_by_group:
    input:
        blast_summaries = lambda wildcards: expand(
            "results/rRNA_analysis/blast/{condition}-{replicate}_{seq_platform}/{gene}.blast.summary",
            condition=wildcards.condition,
            replicate=get_replicates_for_combo(wildcards.condition, wildcards.seq_platform),
            seq_platform=wildcards.seq_platform,
            gene=config.get("target_genes", ["GQX67_05945"])
        )
    output:
        plot_dir = directory("results/rRNA_analysis/plots/blast_{condition}_{seq_platform}")
    params:
        script = config["plot_blast_script"]
    log:
        "logs/plot_blast/{condition}_{seq_platform}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 8000,
        slurm_time = "1:00:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Plotting BLAST results for {wildcards.condition}_{wildcards.seq_platform}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        mkdir -p {output.plot_dir}
        
        # Plot each summary file
        for summary in {input.blast_summaries}; do
            echo "Processing $(basename $summary)..."
            python {params.script} "$summary" --output-dir {output.plot_dir}
        done
        
        echo "BLAST plotting complete"
        """

# Optional: Extract highly represented 16S sequences
rule extract_abundant_16s:
    input:
        blast = "results/rRNA_analysis/blast/{sample_id}/{gene}.blast",
        summary = "results/rRNA_analysis/blast/{sample_id}/{gene}.blast.summary"
    output:
        ids = "results/rRNA_analysis/abundant_16S/{sample_id}/{gene}_abundant_ids.txt",
        fasta = "results/rRNA_analysis/abundant_16S/{sample_id}/{gene}_abundant.fasta"
    params:
        db = config["blast_db"],
        min_reads = config.get("min_blast_reads", 100)
    log:
        "logs/extract_abundant/{sample_id}_{gene}.log"
    threads: 1
    resources:
        slurm_partition = "medium",
        mem_mb = 4000,
        slurm_time = "30:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Extracting abundant 16S sequences for {wildcards.sample_id} - {wildcards.gene}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}
        
        # Extract IDs with at least min_reads reads
        awk -v min={params.min_reads} '$1 >= min {{print $2}}' {input.summary} > {output.ids}
        
        # Extract sequences from BLAST database
        if [ -s {output.ids} ]; then
            blastdbcmd -db {params.db} \
                -entry_batch {output.ids} \
                -out {output.fasta}
        else
            echo "No abundant sequences found" > {output.fasta}
        fi
        
        echo "Abundant sequence extraction complete"
        """

rule integrate_uninfected:
    input:
        files = expand("results/filtered_h5ad/{sample_id}.h5ad", sample_id=[s for s in SAMPLE_IDS if "wMel" not in s and "wRi" not in s]),
    output:
        integrated = "results/integrated/integrated_uninfected.h5ad",
    params:
        files        = "results/filtered_h5ad/*DOX-Ctrl*.h5ad",
        script       = config["integrate_script"],
        sample       = "JW18DOX-Ctrl",
        fig_dir      = "results/integrated/figures_uninfected",
        out_path     = "results/integrated/integrated_uninfected.h5ad",
        resolution   = '0.3',
    log:
        "logs/integrate/integrate_uninfected.log"   
    threads:
        config.get("integrate_threads", 16)
    resources:
        slurm_partition = config.get("integrate_partition", "medium"),
        mem_mb          = config.get("integrate_mem", 128000),
        slurm_time      = config.get("integrate_time", "8:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting integration of uninfected samples"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --files {params.files} \
            --sample {params.sample} \
            --batch_key batch \
            --min_cells 3 \
            --min_genes 200 \
            --n_pcs 30 \
            --resolution {params.resolution} \
            --out_path {params.out_path} \
            --fig_dir {params.fig_dir} \
            --resolution {params.resolution} 
            
        echo "Integration of uninfected samples complete"
        """

rule integrate: 
    input:
        files = expand("results/filtered_h5ad/{sample_id}.h5ad", sample_id=SAMPLE_IDS)
    output:
        integrated = "results/integrated/integrated.h5ad"
    params:
        files         = "results/filtered_h5ad/*.h5ad",   
        script        = config["integrate_script"],
        sample        = "wolbachia_infection",
        fig_dir       = "results/integrated/figures",
        out_path      = "results/integrated/integrated.h5ad",
        resolution    = 0.2,
        bio_condition = config.get("integrate_bio_condition", "")
    log:
        "logs/integrate/integrate.log"
    threads:
        config.get("integrate_threads", 16)
    resources:
        slurm_partition = config.get("integrate_partition", "medium"),
        mem_mb          = config.get("integrate_mem", 128000),
        slurm_time      = config.get("integrate_time", "8:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting integration"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --files {input.files} \
            --sample {params.sample} \
            --batch_key batch \
            --min_cells 3 \
            --min_genes 700 \
            --n_pcs 30 \
            --resolution {params.resolution} \
            --out_path {params.out_path} \
            --fig_dir {params.fig_dir} 
            
        echo "Integration complete"
        """

rule cell_cycle_analysis_uninfected:
    input:
        h5ad = "results/integrated/integrated_uninfected.h5ad",
    output:
        annotated_h5ad = "results/integrated/integrated_uninfected_with_cellcycle.h5ad",
    params:
        script = config["cell_cycle_script"]
    log:
        "logs/cellcycle/cyclum_uninfected.log"
    threads:
        config["cell_cycle_threads"]
    resources:
        slurm_partition = config["cell_cycle_partition"],
        mem_mb = config["cell_cycle_mem"],
        slurm_time = config["cell_cycle_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cell cycle annotation for Uninfected samples"
        echo "Input file: {input.h5ad}"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {CYCLUM_ENV}

        python {params.script} \
            --input {input.h5ad} \
            --output {output.annotated_h5ad}
        
        """

rule cyclum_analysis_uninfected:
    input:
        h5ad = "results/integrated/integrated_uninfected_with_cellcycle.h5ad",
    output:
        annotated    = directory("results/integrated/integrated_uninfected_with_cellcycle_annotated"),
        result       = "results/integrated/integrated_uninfected_with_cellcycle_annotated/JW18_uninfected_cyclum_annotated.h5ad",
        # umap_dir     = directory("results/integrated/integrated_uninfected_with_cellcycle_annotated/JW18_uninfected_umap_per_gene"),
        # cc_stats     = "results/integrated/integrated_uninfected_with_cellcycle_annotated/JW18_uninfected_cc_cluster_stats.csv",
        # de_genes     = "results/integrated/integrated_uninfected_with_cellcycle_annotated/JW18_uninfected_validation_de_genes.csv",
    params:
        script       = config["cyclum_analysis_script"],
        n_top_genes  = config.get("cyclum_n_top_genes", 5),
        n_umap_genes = config.get("cyclum_n_umap_genes", 6),
    log:
        "logs/cellcycle/cyclum_uninfected.log"
    threads:
        config["cell_cycle_threads"]
    resources:
        slurm_partition = config["cell_cycle_partition"],
        mem_mb          = config["cell_cycle_mem"],
        slurm_time      = config["cell_cycle_time"]
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cell cycle annotation for Uninfected samples"
        echo "Input file: {input.h5ad}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {CYCLUM_ENV}

        python {params.script} \
            --input {input.h5ad} \
            --output {output.annotated} \
            --sample JW18_uninfected \
            --save-h5ad \
            --skip-cyclum \
            --n-top-genes {params.n_top_genes} \
            --n-umap-genes {params.n_umap_genes}
        """

rule cell_cycle_analysis:
    input:
        h5ad = rules.integrate.output.integrated
    output:
        annotated_h5ad = "results/integrated/integrated_with_cellcycle.h5ad",
        flag           = touch("results/cellcycle/.done")
    params:
        script  = config["cellcycle_script"],
        fig_dir = "results/cellcycle",
        titer_col  = config.get("continuous_var", "wolbachia_titer")
    log:
        "logs/cellcycle/cellcycle.log"
    threads:
        config.get("cellcycle_threads", 4)
    resources:
        slurm_partition = config.get("cellcycle_partition", "medium"),
        mem_mb          = config.get("cellcycle_mem", 32000),
        slurm_time      = config.get("cellcycle_time", "2:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cell cycle analysis"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --input        {input.h5ad} \
            --output       {params.fig_dir} \
            --sample       wMel \
            --titer-analysis --titer-col {params.titer_col} \
            --save-output

        echo "Cell cycle analysis complete"
        """

rule project_to_cell_cycle:
    input:
        query_h5ad = "results/integrated/integrated_with_cellcycle.h5ad",
        ref_h5ad   = "results/integrated/integrated_uninfected_with_cellcycle.h5ad"
    output:
        projected_h5ad = "results/integrated/integrated_by_cellcycle.h5ad",
        flag           = touch("results/cellcycle_projection/.done")
    params:
        script = config["project_cell_cycle_script"]
    log:
       "logs/cellcycle/projection.log"
    threads:
        config.get("cellcycle_projection_threads", 4)
    resources:
        slurm_partition = config.get("cellcycle_projection_partition", "medium"),
        mem_mb          = config.get("cellcycle_projection_mem", 32000),
        slurm_time      = config.get("cellcycle_projection_time", "2:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cell cycle projection"   

        mamba activate scanpy 

        python {params.script} \
            --query {input.query_h5ad} \
            --ref {input.ref_h5ad} \
            --out_path {output.projected_h5ad}
        """

rule cluster_marker_pathway:
    input:
        h5ad    = "results/integrated/integrated_with_cellcycle.h5ad",
        mapping = config["transcripts_to_genes"]
    output:
        markers_top50 = "results/cluster_marker_pathway/wolbachia_infection_markers_top50.csv",
    params:
        script     = config["cluster_marker_pathway_script"],
        output_dir = "results/cluster_marker_pathway",
        sample     = "wolbachia_infection",          # ← add this
        method     = config.get("cluster_marker_de_method", "wilcoxon"),
        top_n      = config.get("cluster_marker_top_n", 100)
    log:
        "logs/cluster_marker_pathway.log"
    threads:
        config.get("cluster_marker_threads", 8)
    resources:
        slurm_partition = config.get("cluster_marker_partition", "medium"),
        mem_mb          = config.get("cluster_marker_mem", 64000),
        slurm_time      = config.get("cluster_marker_time", "8:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting cluster marker and pathway analysis for {params.sample}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --input   {input.h5ad} \
            --output  {params.output_dir} \
            --sample  {params.sample} \
            --mapping {input.mapping} 
            
        echo "Cluster marker and pathway analysis complete"
        """

rule run_sceptic:
    input:
        h5ad = "results/nmf_programs/adata_with_programs.h5ad",
    output:
        results = "results/sceptic/{sample}/sceptic_results_{sample}.csv",
        stats   = "results/sceptic/{sample}/sceptic_stats_{sample}.csv",
        h5ad    = "results/sceptic/{sample}/sceptic_{sample}.h5ad", 
        flag    = touch("results/sceptic/{sample}/.done")
    params:
        script        = config["sceptic_script"],
        fig_dir       = "results/sceptic/{sample}",
        pca_key       = config.get("sceptic_pca_key", "X_pca_harmony"),
        timepoint_col = config.get("sceptic_timepoint_col", "timepoint_numeric"),
        method        = config.get("sceptic_method", "xgboost"),
        n_bins        = config.get("sceptic_n_bins", 10)
    log:
        "logs/sceptic/{sample}.log"
    threads:
        config.get("sceptic_threads", 8)
    resources:
        slurm_partition = config.get("sceptic_partition", "medium"),
        mem_mb          = config.get("sceptic_mem", 64000),
        slurm_time      = config.get("sceptic_time", "4:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting SCEPTIC analysis for sample: {wildcards.sample}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --h5ad          {input.h5ad} \
            --sample        {wildcards.sample} \
            --fig_dir       {params.fig_dir} \
            --pca_key       {params.pca_key} \
            --timepoint_col {params.timepoint_col} \
            --method        {params.method} \
            --n_bins        {params.n_bins}

        echo "SCEPTIC analysis complete for {wildcards.sample}"
        """
rule pseudotime_gene_importance:
    input:
        h5ad        = "results/sceptic/{sample}/sceptic_{sample}.h5ad",
        done        = "results/sceptic/{sample}/.done",
        program_dir = "results/nmf_programs/.done"
    output:
        summary  = "results/pseudotime_genes/{sample}/summary_dynamic_genes.csv",
        spearman = "results/pseudotime_genes/{sample}/spearman_sig.csv",
        counts   = "results/pseudotime_genes/{sample}/tradeseq_inputs/counts_genesXcells.csv",
        pt       = "results/pseudotime_genes/{sample}/tradeseq_inputs/pseudotime.csv",
        flag     = touch("results/pseudotime_genes/{sample}/.done")
    params:
        script      = config.get("pseudotime_gene_script",
                          "scripts/method_comparison/pseudotime_gene_importance.py"),
        outdir      = "results/pseudotime_genes/{sample}",
        program_dir = "results/nmf_programs",
    log:
        "logs/pseudotime_genes/{sample}.log"
    threads:
        config.get("pseudotime_gene_threads", 8)
    resources:
        slurm_partition = config.get("pseudotime_gene_partition", "medium"),
        mem_mb          = config.get("pseudotime_gene_mem", 64000),
        slurm_time      = config.get("pseudotime_gene_time", "2:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting pseudotime gene importance for {wildcards.sample}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --h5ad        {input.h5ad} \
            --outdir      {params.outdir} \
            --program-dir {params.program_dir}

        echo "Pseudotime gene importance complete for {wildcards.sample}"
        """


##################################################################
# Gene program and pathway analysis rules
##################################################################
rule nmf_programs:
    input:
        h5ad = "results/integrated/integrated_with_cellcycle.h5ad"
    output:
        adata_with_programs = "results/nmf_programs/adata_with_programs.h5ad",
        summary = "results/nmf_programs/SUMMARY.txt",
        flag = touch("results/nmf_programs/.done")
    params:
        script = config.get("nmf_script", "../snakemake_scripts/analysis/nmf_programs.py"),
        output_dir = "results/nmf_programs",
        n_programs = config.get("n_programs", 15),
        n_top_genes = config.get("n_top_genes", 2000),
        organism = config.get("organism", "Fly"),
        gene_id_type = config.get("gene_id_type", "flybase"),
        flybase_annotation = config.get("flybase_annotation",
            "../reference/fbgn_annotation_ID_fb_2025_04.tsv.gz")
    log:
        "logs/nmf/nmf_programs.log"
    threads:
        config.get("nmf_threads", 8)
    resources:
        slurm_partition = config.get("nmf_partition", "medium"),
        mem_mb          = config.get("nmf_mem", 64000),
        slurm_time      = config.get("nmf_time", "4:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting NMF program discovery"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --input {input.h5ad} \
            --output_dir {params.output_dir} \
            --n_programs {params.n_programs} \
            --n_top_genes {params.n_top_genes} \
            --organism {params.organism} \
            --gene_id_type {params.gene_id_type} \
            --flybase_annotation {params.flybase_annotation}

        echo "NMF program discovery complete"
        """

rule nmf_continuous_var:
    input:
        adata_with_programs = "results/nmf_programs/adata_with_programs.h5ad"
    output:
        correlations = "results/nmf_continuous_var/program_correlations.csv",
        summary = "results/nmf_continuous_var/SUMMARY.txt",
        flag = touch("results/nmf_continuous_var/.done")
    params:
        script = config.get("nmf_continuous_script", 
            "../snakemake_scripts/analysis/nmf_continuous_var.py"),
        output_dir = "results/nmf_continuous_var",
        continuous_var = config.get("continuous_var", None),  # Auto-detect if None
        flybase_annotation = config.get("flybase_annotation", 
            "reference/fbgn_annotation_ID_fb_2025_04.tsv.gz")
    log:
        "logs/nmf/nmf_continuous_var.log"
    threads:
        config.get("nmf_continuous_threads", 4)
    resources:
        slurm_partition = config.get("nmf_continuous_partition", "medium"),
        mem_mb = config.get("nmf_continuous_mem", 32000),
        slurm_time = config.get("nmf_continuous_time", "2:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting NMF continuous variable analysis"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}
        
        python {params.script} \
            --input {input.adata_with_programs} \
            --output_dir {params.output_dir} \
            --continuous_var {params.continuous_var} \
            --flybase_annotation {params.flybase_annotation}
        
        echo "NMF continuous variable analysis complete"
        """

rule nmf_categorical_var:
    input:
        adata_with_programs = "results/nmf_programs/adata_with_programs.h5ad"
    output:
        comparison = "results/nmf_categorical_var/program_comparison.csv",
        flag = touch("results/nmf_categorical_var/.done")
    params:
        script = config.get("nmf_categorical_script", 
            "../snakemake_scripts/analysis/nmf_categorical_var.py"),
        output_dir = "results/nmf_categorical_var",
        categorical_var = config.get("categorical_var", None)  # Auto-detect if None
    log:
        "logs/nmf/nmf_categorical_var.log"
    threads:
        config.get("nmf_categorical_threads", 4)
    resources:
        slurm_partition = config.get("nmf_categorical_partition", "medium"),
        mem_mb = config.get("nmf_categorical_mem", 32000),
        slurm_time = config.get("nmf_categorical_time", "2:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting NMF categorical variable analysis"
        
        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}
        
        python {params.script} \
            --input {input.adata_with_programs} \
            --output_dir {params.output_dir} \
            --categorical_var {params.categorical_var}
        
        echo "NMF categorical variable analysis complete"
        """

rule nmf_annotate_programs:
    input:
        adata_with_programs = "results/nmf_programs/adata_with_programs.h5ad",
        nmf_done            = "results/nmf_programs/.done",
        mapping             = config["transcripts_to_genes"]
    output:
        output  = "results/nmf_annotate_programs/program_cellcycle_overlap.csv",
    params:
        script       = config.get("nmf_annotate_script",
                           "../snakemake_scripts/analysis/annotate_nmf_programs.py"),
        output_dir   = "results/nmf_annotate_programs",
        program_dir  = "results/nmf_programs",
        sample_name  = config.get("nmf_annotate_sample", "wolbachia_infection"),
        titer_var    = config.get("continuous_var", "wolbachia_titer"),
        cc_s_var     = config.get("cc_s_var",      "S_score"),
        cc_g2m_var   = config.get("cc_g2m_var",    "G2M_score"),
        cc_phase_var = config.get("cc_phase_var",  "phase"),
        top_genes    = config.get("nmf_annotate_top_genes", 200),
        skip_gsea    = "--skip_gsea" if config.get("nmf_annotate_skip_gsea", False) else "",
        skip_fly     = "--skip_flyenrichr" if config.get("nmf_annotate_skip_flyenrichr", False) else ""
    log:
        "logs/nmf/nmf_annotate_programs.log"
    threads:
        config.get("nmf_annotate_threads", 8)
    resources:
        slurm_partition = config.get("nmf_annotate_partition", "medium"),
        mem_mb          = config.get("nmf_annotate_mem",       32000),
        slurm_time      = config.get("nmf_annotate_time",      "4:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting NMF program annotation"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        python {params.script} \
            --input        {input.adata_with_programs} \
            --program_dir  {params.program_dir} \
            --output_dir   {params.output_dir} \
            --mapping      {input.mapping} \
            --titer_var    {params.titer_var} \
            --cc_s_var     {params.cc_s_var} \
            --cc_g2m_var   {params.cc_g2m_var} \
            --cc_phase_var {params.cc_phase_var} \
            --top_genes    {params.top_genes} \
            {params.skip_gsea} \
            {params.skip_fly}

        echo "NMF program annotation complete"
        """
rule run_tradeseq:
    input:
        counts = "results/pseudotime_genes/{sample}/tradeseq_inputs/counts_genesXcells.csv",
        pt     = "results/pseudotime_genes/{sample}/tradeseq_inputs/pseudotime.csv",
    output:
        assoc  = "results/pseudotime_genes/{sample}/tradeseq_association.csv",
        sve    = "results/pseudotime_genes/{sample}/tradeseq_startvsend.csv",
        sce    = "results/pseudotime_genes/{sample}/tradeseq_sce.rds",
        flag   = touch("results/pseudotime_genes/{sample}/tradeseq.done")
    params:
        script = "scripts/method_comparison/run_tradeseq.R",
        outdir = "results/pseudotime_genes/{sample}",
        nknots = config.get("tradeseq_nknots", 6),
    log:
        "logs/tradeseq/{sample}.log"
    threads:
        config.get("tradeseq_threads", 16)
    resources:
        slurm_partition = config.get("tradeseq_partition", "medium"),
        mem_mb          = config.get("tradeseq_mem", 128000),
        slurm_time      = config.get("tradeseq_time", "12:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting tradeSeq for {wildcards.sample}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        Rscript {params.script} \
            --counts   {input.counts} \
            --pt       {input.pt} \
            --outdir   {params.outdir} \
            --nknots   {params.nknots} \
            --nworkers {threads}

        echo "tradeSeq complete for {wildcards.sample}"
        """

"""
# Testing commands for new rules:
python scripts/method_comparison/validate_pipseq.py \
    --files results/annotated_h5ad/*.h5ad \
    --out_path results/method_comparison/all_conditions_all_methods.h5ad \
    --fig_dir results/method_comparison/all_conditions \
    --sample all_conditions \
    --batch_key batch \
    --min_cells 3 \
    --min_genes 200 \
    --n_pcs 30


python scripts/method_comparison/cell_cycle_association.py \
    --input results/method_comparison/all_conditions_all_methods.h5ad \
    --output results/cellcycle_analysis \
    --sample all_conditions \
    --run-cyclum \
    --force-retrain \
    --save-output
"""

# Count reads aligning to Wolbachia 16S rRNA (GQX67_05945) vs total reads per sample
rule count_16s_reads:
    input:
        r2  = lambda wildcards: get_fastq_files(wildcards.sample_id)[1],
        ref = config["ref_fasta"]
    output:
        counts = "results/rRNA_analysis/read_counts/{sample_id}/{gene}_read_counts.txt"
    wildcard_constraints:
        # Only allow sample_ids that currently exist in the dataframe
        sample_id = "|".join(SAMPLE_IDS)
    params:
        region = config.get("rRNA_16S_region", "GQX67_05945::NZ_CP046925.1:1167785-1169290")
    log:
        "logs/count_16s/{sample_id}_{gene}.log"
    threads: 16
    resources:
        slurm_partition = "medium",
        mem_mb          = 32000,
        slurm_time      = "2:00:00"
    shell:
        """
        exec > {log} 2>&1
        echo "Counting 16S vs total reads for {wildcards.sample_id} - {wildcards.gene}"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SRA_TOOLS_ENV}

        mkdir -p results/rRNA_analysis/read_counts/{wildcards.sample_id}

        SORTED=results/rRNA_analysis/read_counts/{wildcards.sample_id}/all_aligned.bam

        # Align R2 to the combined Dmel + wMel rRNA reference, sort, index
        bwa mem -t {threads} {input.ref} {input.r2} | \
            samtools view -Sb | \
            samtools sort -@ {threads} -o $SORTED
        samtools index $SORTED

        # Reads mapped to the 16S region (primary alignments only, excludes
        # unmapped/secondary/supplementary via -F 0x904)
        SIXTEEN_S=$(samtools view -c -F 0x904 $SORTED "{params.region}")

        # Total reads in the input (all records in the BAM)
        TOTAL=$(samtools view -c $SORTED)

        # Mapped reads only (optional denominator if you prefer mapped over total)
        MAPPED=$(samtools view -c -F 0x4 $SORTED)

        # Write tab-delimited summary
        printf "sample\\tgene\\tregion\\tsixteenS_reads\\ttotal_reads\\tmapped_reads\\n" > {output.counts}
        printf "%s\\t%s\\t%s\\t%s\\t%s\\t%s\\n" \
            "{wildcards.sample_id}" "{wildcards.gene}" "{params.region}" \
            "$SIXTEEN_S" "$TOTAL" "$MAPPED" >> {output.counts}

        echo "Done. 16S=$SIXTEEN_S  total=$TOTAL  mapped=$MAPPED"
        """

rule validate_platform_concordance:
    input:
        h5ad = "results/integrated/integrated.h5ad"
    output:
        confusion  = "results/validate_pipseq/label_transfer_confusion_matrix.csv",
        jaccard    = "results/validate_pipseq/marker_gene_jaccard_matrix.csv",
        pseudobulk = "results/validate_pipseq/pseudobulk_spearman_correlation.csv"
    params:
        script          = config.get("validate_platform_script",
                              "snakemake_scripts/analysis/validate_platform_concordance.py"),
        outdir          = "results/validate_pipseq",
        embedding       = config.get("validate_platform_embedding", "X_pca_harmony"),
        cluster_key     = config.get("validate_platform_cluster_key", "leiden"),
        platform_key    = config.get("validate_platform_method_key", "method"),
        n_markers       = config.get("validate_platform_n_markers", 50),
        n_neighbors_knn = config.get("validate_platform_knn_neighbors", 15),
        counts_source   = config.get("validate_platform_counts_source", "raw")
    log:
        "logs/validate_platform/validate_platform_concordance.log"
    threads:
        config.get("validate_platform_threads", 8)
    resources:
        slurm_partition = config.get("validate_platform_partition", "medium"),
        mem_mb          = config.get("validate_platform_mem", 64000),
        slurm_time      = config.get("validate_platform_time", "4:00:00")
    shell:
        """
        exec > {log} 2>&1
        echo "Starting platform concordance validation"

        source $(dirname $(dirname $(which conda)))/etc/profile.d/conda.sh
        conda activate {SCANPY_ENV}

        mkdir -p {params.outdir}

        python {params.script} \
            --h5ad {input.h5ad} \
            --outdir {params.outdir} \
            --embedding {params.embedding} \
            --cluster_key {params.cluster_key} \
            --platform_key {params.platform_key} \
            --n_markers {params.n_markers} \
            --n_neighbors_knn {params.n_neighbors_knn} \
            --counts_source {params.counts_source}

        echo "Platform concordance validation complete"
        """
